plugins {
    id "com.android.application" version "8.0.2"
    id "com.ioki.sentry.proguard"
}

android {
    namespace 'com.ioki.sentry.proguard.plugin.test.project'
    compileSdk 33

    defaultConfig {
        applicationId "com.ioki.sentry.proguard.plugin.test.project"

        minSdk 29
        targetSdk 33

        versionName '0000-00-00.0'
        versionCode 1
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
        production {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
        staging {
            minifyEnabled false
        }
    }

    flavorDimensions = ["partner"]
    def partners = ["a", "b", "c"]
    partners.forEach { partnerName ->
        productFlavors.register(partnerName) {
            dimension "partner"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

sentryProguard {
    organization.set("sentryOrg")
    project.set("sentryProject")
    authToken.set("sentryAuthToken")
    noUpload.set(
            providers.gradleProperty("IOKI_SENTRY_NO_UPLOAD")
                    .map { it.toBoolean() }
                    .orElse(false)
    )
}